pipeline {
    agent any

    environment {
        // ===================== åŸºç¡€ç‰ˆæœ¬ä¿¡æ¯ =====================
        BUILD_NAME             = "${env.BUILD_NAME ?: '1.0.0'}"
        IOS_BUILD_NUMBER       = "${env.IOS_BUILD_NUMBER ?: '1'}"
        ANDROID_BUILD_NUMBER   = "${env.ANDROID_BUILD_NUMBER ?: '1'}"

        // ===================== è·¯å¾„ä¸é…ç½® =====================
        EXPORT_PATH            = "${WORKSPACE}/pack_store"
        EXPORT_OPTIONS_PLIST_PATH = "${EXPORT_PATH}/export_options_appStore.plist"
        DART_DEFINE_FILE       = "${EXPORT_PATH}/prod.env"
        OUTPUT_DIR             = "/Users/agi00107/Desktop/outpackage/market_release"
        ARCHIVE_OUTPUT_PATH    = "${OUTPUT_DIR}/${BUILD_NAME}/ios"
        APK_OUTPUT_PATH        = "${OUTPUT_DIR}/${BUILD_NAME}/apk"

        // ===================== Git ä»“åº“ =====================
        GIT_REPO               = 'git@codeup.aliyun.com:6710bdc09d3c82efe37b13cc/facesong/facesong_flutter.git'
        GIT_CREDENTIAL_ID      = 'git-ssh-key'
        GIT_REF                = "${env.GIT_REF ?: 'main'}"

        // ===================== APK åŠ å›ºé…ç½® =====================
        PROTECT_APK            = "${env.PROTECT_APK ?: 'true'}"
        SECAPI_JAR_PATH        = "${EXPORT_PATH}/secapi-4.1.5-SNAPSHOT.jar"
        CHANNEL_FILE           = "${EXPORT_PATH}/channelname.txt" 
        UNZIP_PATH             = "${EXPORT_PATH}/jyzip.sh"

        // ===================== é’‰é’‰å‘Šè­¦ =====================
        DINGTALK_WEBHOOK       = "https://oapi.dingtalk.com/robot/send?access_token=057c702cdb1896282659cd07439846fd07ec052cf599883260c08f289f2cd89f"
    }

    stages {
        stage('åˆå§‹åŒ–ä»“åº“') {
            steps {
                dir('facesong_flutter') {
                    echo "ğŸš€ [1/7] åˆå§‹åŒ– Git ä»“åº“..."
                    script {
                        def gitDirExists = fileExists('.git')
                        if (!gitDirExists) {
                            deleteDir()
                            sh "git clone ${env.GIT_REPO} . && git checkout ${env.GIT_REF}"
                        } else {
                            sh "git reset --hard && git clean -fd"
                        }

                        checkout([$class: 'GitSCM',
                            branches: [[name: "${env.GIT_REF}"]],
                            doGenerateSubmoduleConfigurations: false,
                            extensions: [[$class: 'CleanBeforeCheckout']],
                            userRemoteConfigs: [[
                                url: env.GIT_REPO,
                                credentialsId: env.GIT_CREDENTIAL_ID
                            ]]
                        ])
                        sh "git log -5 --pretty=format:'%h %an %ad %s' --date=short"
                    }
                }
            }
        }

        stage('ç¡®è®¤ç‰ˆæœ¬å·') {
            steps {
                echo "ğŸ”¢ [2/7] ç‰ˆæœ¬å·ç¡®è®¤ï¼šBUILD_NAME=${BUILD_NAME}, IOS_BUILD_NUMBER=${IOS_BUILD_NUMBER}, ANDROID_BUILD_NUMBER=${ANDROID_BUILD_NUMBER}"
            }
        }

        stage('Flutter åˆå§‹åŒ–') {
            steps {
                dir('facesong_flutter') {
                    sh """
                        set -e
                        rm -f pubspec.lock ios/Podfile.lock
                        fvm flutter clean
                        export PUB_HOSTED_URL=https://pub.flutter-io.cn
                        fvm flutter pub get
                    """
                }
            }
        }

        stage('æ„å»º iOS IPA') {
            when { expression { return env.BUILD_IOS == "true" } }
            steps {
                dir('facesong_flutter') {
                    script {
                        def iosBuildResult = sh(
                            script: """
                                set -e
                                sh build.sh ipa \
                                    --channel AppStore \
                                    --flavor production \
                                    --export-options-plist=${EXPORT_OPTIONS_PLIST_PATH} \
                                    --dart-define-from-file=\${DART_DEFINE_FILE} \
                                    --dart-define=WATERMARK=false \
                                    --dart-define=DEV_CONFIG=false \
                                    --build-name ${BUILD_NAME} \
                                    --build-number ${IOS_BUILD_NUMBER}
                            """,
                            returnStatus: true
                        )

                        if (iosBuildResult != 0) {
                            sendDingTalkMessage(
                                "iOS æ‰“åŒ…å¤±è´¥",
                                generateMarkdown("iOS", "âŒ iOS æ„å»ºå¤±è´¥", BUILD_NAME, IOS_BUILD_NUMBER, env.GIT_REF)
                            )
                            error("iOS æ„å»ºå¤±è´¥")
                        }
                    }
                }
            }
        }

        stage('å¤åˆ¶ xcarchive') {
            when { expression { return env.BUILD_IOS == "true" } }
            steps {
                dir('facesong_flutter') {
                    script {
                        def sourceDir = "build/ios/archive/Runner.xcarchive"
                        def targetDir = "${ARCHIVE_OUTPUT_PATH}/Runner.xcarchive"
                        sh """
                            set -e
                            mkdir -p "${ARCHIVE_OUTPUT_PATH}"
                            if [ -d "${sourceDir}" ]; then
                                rm -rf "${targetDir}"
                                cp -R "${sourceDir}" "${targetDir}"
                            else
                                exit 1
                            fi
                        """
                        // âœ… iOS æˆåŠŸé€šçŸ¥
                        def markdownText = generateMarkdown("iOS", "âœ… iOS æ„å»ºå®Œæˆ", BUILD_NAME, IOS_BUILD_NUMBER, env.GIT_REF)
                        sendDingTalkMessage("iOS æ‰“åŒ…å®Œæˆ", markdownText)
                    }
                }
            }
        }

        stage('æ„å»º & åŠ å›º Android APK') {
            when { expression { return env.BUILD_ANDROID == "true" } }
            steps {
                dir('facesong_flutter') {
                    script {
                        sh "mkdir -p ${APK_OUTPUT_PATH}"

                        sh """
                            set -e
                            cp "${EXPORT_PATH}/key.properties" android/app/key.properties
                            cp "${EXPORT_PATH}/release.keystore" android/app/release.keystore
                        """

                        echo "ğŸŸ¢ å¼€å§‹æ„å»º APK"

                        def buildResult = sh(
                            script: """
                                set -e
                                if sed --version >/dev/null 2>&1; then
                                    sed -i 's/minSdk = flutter\\.minSdkVersion/minSdk = 24/' android/app/build.gradle
                                else
                                    sed -i '' 's/minSdk = flutter\\.minSdkVersion/minSdk = 24/' android/app/build.gradle
                                fi

                                fvm flutter build apk \
                                    --flavor production \
                                    --release \
                                    --dart-define-from-file="\${DART_DEFINE_FILE}" \
                                    --dart-define=WATERMARK=false \
                                    --dart-define=DEV_CONFIG=false \
                                    --build-name="${BUILD_NAME}" \
                                    --build-number="${ANDROID_BUILD_NUMBER}"
                            """,
                            returnStatus: true
                        )

                        if (buildResult != 0) {
                            error("âŒ APK æ„å»ºå¤±è´¥")
                        }

                        def builtApk = "build/app/outputs/flutter-apk/app-production-release.apk"
                        if (!fileExists(builtApk)) {
                            error("âŒ æœªæ‰¾åˆ° APK æ–‡ä»¶: ${builtApk}")
                        }

                        // APK åŠ å›º
                        if (env.PROTECT_APK == "true") {
                            echo "ğŸ”’ å¼€å§‹åŠ å›º APK"
                            def protectResult = sh(
                                script: """
                                    set -e
                                    java -jar "${SECAPI_JAR_PATH}" \
                                        -i 10.200.18.111:8000 \
                                        -u zyljsh \
                                        -a 3e41fc10-8c1b-44c2-9c1c-d3a99b1330ca \
                                        -c ba2749da-6086-41cd-b801-ee75727c4bdd \
                                        -f 0 -t 100000 \
                                        -p "${builtApk}" -d "${APK_OUTPUT_PATH}" \
                                        --action ud --ks 1 -l "${CHANNEL_FILE}"
                                """,
                                returnStatus: true
                            )

                            if (protectResult != 0) {
                                error("âŒ APK åŠ å›ºå¤±è´¥")
                            }
                            echo "âœ… APK åŠ å›ºæˆåŠŸ"
                        } else {
                            echo "âš™ï¸ æœªå¼€å¯åŠ å›ºï¼Œç›´æ¥å¤åˆ¶æ„å»ºäº§ç‰©"
                            sh "cp -v ${builtApk} ${APK_OUTPUT_PATH}/app-production-release.apk"
                        }
                    }
                }
            }
        }

        stage('å‹ç¼© & è§£å‹ & æ‹†åˆ†æ¸ é“ APK') {
            when { expression { return env.BUILD_ANDROID == "true" } }
            steps {
                dir('facesong_flutter') {
                    script {
                        echo "ğŸ“‚ è°ƒç”¨ è§£å‹ è„šæœ¬å¤„ç† APK"
                        sh """
                            sh ${UNZIP_PATH} ${APK_OUTPUT_PATH} ${BUILD_NAME} ${ANDROID_BUILD_NUMBER}
                        """
                        echo "âœ… APK å·²æŒ‰æ¸ é“æ‹†åˆ†å¹¶é‡å‘½åå®Œæˆ"

                        // âœ… Android æˆåŠŸé€šçŸ¥
                        def markdownText = generateMarkdown("Android", "âœ… Android æ„å»ºå®Œæˆ", BUILD_NAME, ANDROID_BUILD_NUMBER, env.GIT_REF)
                        sendDingTalkMessage("Android æ‰“åŒ…å®Œæˆ", markdownText)
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                def markdownText = """
### âŒ Jenkins æ„å»ºå¤±è´¥

- **build_version**ï¼š${BUILD_NAME}
- **build_number**ï¼šiOS: ${IOS_BUILD_NUMBER}, Android: ${ANDROID_BUILD_NUMBER}
- **æ„å»ºåˆ†æ”¯**ï¼š${env.GIT_REF ?: 'æœªçŸ¥'}
- **å®Œæˆæ—¶é—´**ï¼š${new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("Asia/Shanghai"))}
                """.stripIndent()
                sendDingTalkMessage("æ„å»ºå¤±è´¥", markdownText)
            }
        }
    }
}

// ---------------- è‡ªå®šä¹‰å‡½æ•° ----------------
def sendDingTalkMessage(String title, String content) {
    sh """
        curl -s '${DINGTALK_WEBHOOK}' \
        -H 'Content-Type: application/json' \
        -d '{
            "msgtype": "markdown",
            "markdown": {
                "title": "${title}",
                "text": "${content}"
            }
        }'
    """
}

// ---------------- Markdown æ„å»ºå‡½æ•° ----------------
def generateMarkdown(String platform, String resultText, String buildVersion, String buildNumber, String gitRef) {
    def timeStr = new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("Asia/Shanghai"))

    return """
### ğŸ‰ Jenkins æ„å»ºå®Œæˆï¼ˆ${platform}ï¼‰

- **äº§ç‰©è·¯å¾„**: [smb://10.200.35.17](smb://10.200.35.17)
- **build_version**ï¼š${buildVersion}
- **build_number**ï¼š${buildNumber}
- **æ„å»ºåˆ†æ”¯**ï¼š${gitRef ?: 'æœªçŸ¥'}
- **å®Œæˆæ—¶é—´**ï¼š${timeStr}

#### ğŸ“¦ æ„å»ºç»“æœ
- ${resultText}
    """.stripIndent()
}

